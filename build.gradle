buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.palantir.gradle.revapi:gradle-revapi:1.5.0'
    }
}

plugins {
    id 'signing'
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
    id "nebula.optional-base" version "3.0.3" apply false
    id 'biz.aQute.bnd.builder' version "$bndVersion" apply false
    id 'org.javamodularity.moduleplugin' version '1.8.10' apply false
}

scmVersion {
    tag {
        prefix = 'ical4j-connector-'
    }
    versionCreator 'versionWithBranch'
    branchVersionCreator = [
            'master': 'simple',
            'feature/refactor': 'simple',
    ]
    nextVersion {
        suffix = 'pre'
        separator = '-'
    }
}

configure(subprojects) {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'groovy'
    apply plugin: 'biz.aQute.bnd.builder'
//    apply plugin: 'org.javamodularity.moduleplugin'
    apply plugin: 'nebula.optional-base'

    repositories {
        mavenCentral()
    }

    sourceCompatibility = 8
    targetCompatibility = 8

    group = 'org.ical4j'
    description = '''\
A Java library for accessing iCalendar data stores
'''
    version = rootProject.scmVersion.version

    ext {
        isReleaseVersion = !version.endsWith("SNAPSHOT")
    }

    dependencies {
        api "org.mnode.ical4j:ical4j:$ical4jVersion",
                "org.mnode.ical4j:ical4j-vcard:$ical4jVCardVersion",
                "org.ical4j:ical4j-serializer:$ical4jSerializerVersion",
                'commons-io:commons-io:2.11.0',
                "org.jooq:jool-java-8:$joolVersion",
                "org.jetbrains:annotations:$jetbrainsVersion"

        implementation "org.codehaus.groovy:groovy:$groovyVersion", optional

        implementation ("org.eclipse.jetty:jetty-server:$jettyVersion") {
            exclude group: 'org.slf4j', module: 'slf4j-api'
        }
        implementation ("org.eclipse.jetty:jetty-servlet:$jettyVersion") {
            exclude group: 'org.slf4j', module: 'slf4j-api'
        }

        implementation "info.picocli:picocli:$picocliVersion"

        compileOnly 'org.osgi:osgi.core:8.0.0',
                'org.osgi:org.osgi.service.component.annotations:1.5.0',
                'org.osgi:org.osgi.service.metatype.annotations:1.4.1',
                'org.osgi:org.osgi.annotation:6.0.0'

        // testcontainers
        testImplementation "org.testcontainers:testcontainers:$testcontainersVersion",
                "org.testcontainers:spock:$testcontainersVersion"

        testImplementation "org.slf4j:slf4j-log4j12:$slf4jVersion",
                "org.apache.logging.log4j:log4j:$log4jVersion"
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set('javadoc')
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    test {
        useJUnitPlatform()
    }

    javadoc {
        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
        options {
            links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        }
    }

    publishing {
        publications {
            "$name"(MavenPublication) {
                from components.java
                artifact javadocJar
                artifact sourcesJar
                pom.withXml {
                    asNode().appendNode('name', name)
                    asNode().appendNode('description', description)
                    asNode().appendNode('url', 'http://ical4j.github.io')

                    def scmNode = asNode().appendNode('scm')
                    scmNode.appendNode('url', 'https://github.com/ical4j/ical4j-connector')
                    scmNode.appendNode('connection', 'scm:git@github.com:ical4j/ical4j-connector.git')
                    scmNode.appendNode('developerConnection', 'scm:git@github.com:ical4j/ical4j-connector.git')

                    def licenseNode = asNode().appendNode('licenses').appendNode('license')
                    licenseNode.appendNode('name', 'iCal4j - License')
                    licenseNode.appendNode('url', 'https://raw.githubusercontent.com/ical4j/ical4j/master/LICENSE')
                    licenseNode.appendNode('distribution', 'repo')

                    def developerNode = asNode().appendNode('developers').appendNode('developer')
                    developerNode.appendNode('id', 'fortuna')
                    developerNode.appendNode('name', 'Ben Fortuna')
                }
            }
        }

        repositories {
            maven {
                name = "OSSRH"
                url = version.endsWith('SNAPSHOT') ? "https://s01.oss.sonatype.org/content/repositories/snapshots/" : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }

    signing {
        required { isReleaseVersion }
        sign publishing.publications[name]
    }
}
